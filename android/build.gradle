// Copyright 2023 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

group 'com.google.maps.flutter.navigation'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '2.0.0'
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.ncorti.ktfmt.gradle:plugin:0.21.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    configurations {
        implementation {
            exclude group: 'com.google.android.gms', module: 'play-services-maps'
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.ncorti.ktfmt.gradle'

ktfmt {
    googleStyle()
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.google.maps.flutter.navigation'
    }

    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 23
        consumerProguardFiles 'proguard.txt'
    }

    dependencies {
        implementation "androidx.car.app:app:1.4.0"
        implementation "androidx.car.app:app-projected:1.4.0"
        implementation "com.google.android.libraries.navigation:navigation:6.1.0"
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'io.mockk:mockk:1.13.8'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.robolectric:robolectric:4.9'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }
}
